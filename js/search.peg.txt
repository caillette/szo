// PEG.js grammar for Szótárgép
// http://pegjs.majda.cz/online

maybeSearch = search : search?
  { return search === '' ? [] : [].concat( search ) }

search =
  first : ( '?' parameter : parameter { return parameter } )
  rest : ( '&' parameter : parameter { return parameter } )*
  { return first ? [ first ].concat( rest ) : []  }


parameter =
    key : 'v' '=' uri : uri { return [ key, uri ] }
  / key : 'tags' '=' tags : tags? { return [ key, tags ? tags : [] ] }
  / key : 'flip' { return [ key ] }
  / key : 'single' { return [ key ] }
  / key : 'lang' '=' language : language { return [ key, language ] }


uri =
  scheme : ( 'http://' / 'https://' / 'file://' )?
  path : path
  { return scheme + path }


path =
  first : segment
  rest : ( separator : '/' segment : segment { return separator + segment } )*
  extension : extension
  { return first + rest.join( '' ) + extension }

segment "Directory or file name" =
  name : [0-9a-zA-Z\-_]+ { return name.join( '' ) }

extension 'Extension (must be ".txt")' = '.txt'


tags =
  first : tag
  rest : ( ';' tag : tag { return tag } )*
  { return first ? [ first ].concat( rest ) : [] }

tag = tag : [0-9a-zA-Z\-_$]+ { return tag.join( '' ) ; }


language = language : [a-z]+ { return language.join( '' ) ; }
